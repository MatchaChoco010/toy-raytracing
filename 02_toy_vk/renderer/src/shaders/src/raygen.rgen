#version 460
#extension GL_EXT_ray_tracing : enable

layout(location = 0) rayPayloadEXT vec3 hitValue;

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;

layout(push_constant) uniform PushConstants {
  mat4 cameraRotate;
  vec3 cameraTranslate;
  float aspectRatio;
}
pushConstants;

void main() {
  const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
  const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
  vec2 d = inUV * 2.0 - 1.0;
  d.x *= pushConstants.aspectRatio;

  vec3 origin = pushConstants.cameraTranslate;
  vec3 direction = normalize(vec3(d.x, d.y, -1.0));
  direction = (pushConstants.cameraRotate * vec4(direction, 0.0)).xyz;
  float tmin = 0.001;
  float tmax = 100000.0;

  traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT,
              0xff, // mask
              0,    // sbtRecordOffset
              0,    // sbtRecordStride
              0,    // missIndex
              origin, tmin, direction, tmax,
              0 // payload location
  );

  vec4 prev = imageLoad(image, ivec2(gl_LaunchIDEXT.xy));
  imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(prev.rgb + hitValue, 1.0));
}
